
?start: program

program: (declaration | macro | module)* expression?

declaration: identifier "::" expression "::"

data: "data" identifier "::" expression

module: "module" NAME "::" declaration* "::"

access: NAME "/" (identifier | access)

macro: "{" identifier splice "}" "::" expression "::"

infix: expression "`" term "`" expression

match: "match" "{" match_case* final_case "}"

match_case: "if" expression "case" expression

final_case: "else" expression

?splice: id_splice
       | head_splice
       | tail_splice

id_splice: "~" NAME
head_splice: "~." NAME
tail_splice: "~@" NAME

expression: term+

ifelse: "{" "if" expression (elif expression)* ("else" expression)* "}"

?elif: "{" "elif" expression "}"

while: "while" expression "loop" expression

combinator: "{" (identifier | splice) (expression | splice)? "}"

array: "[" (term | number)+ "]" | "[" "]"

set_symbol: ":=" NAME

symbol: ":" NAME

?term: while
     | infix
     | combinator
     | ifelse
     | atom

?atom: access
     | identifier
     | string
     | character
     | number
     | array
     | set_symbol
     | symbol
     | "(" expression ")"

number: NUMBER

string: STRING

character: CHARACTER

identifier: NAME | OPERATOR

OPERATOR: /(?<![a-zA-Z])[+\*\/!@#$%&;,?\=\|<>_.^][+\-*\/!@#$%;,?\=\|<>_.^:]*|[+*\/!@#$%^&:;,?~=<>](?![A-Z=:])[+\-*\/!@#$%^&;,?=~<>]*|[\-](?![0-9])((?![a-zA-Z])[+\-*\/!@#$%^&;,?=~<>])*/

NAME: /[A-Z][a-zA-Z0-9]*/

CHARACTER: /'[^']'/
 
STRING: /"[^"]*"/

NUMBER: /-?\d+(\.\d+)?/

%ignore /\s+/
